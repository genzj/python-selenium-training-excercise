*** Settings ***
Resource          ../R01-lib-resource.robot

*** Keywords ***
Fill Form
    [Arguments]    ${name}    ${email}    ${comment}
    s2l.Input Text    id=cname    ${name}
    s2l.Input Text    id=cemail    ${email}
    s2l.Input Text    id=ccomment    ${comment}
    s2l.Click Button    xpath=//input[@class="submit"][1]

Should Message Be
    [Arguments]    ${name msg}    ${email msg}    ${comment msg}
    Run Keyword If    len('${name msg}') > 0    s2l.Element Text Should Be    id=cname-error    ${name msg}
    ...    ELSE    s2l.Element Should Not Be Visible    id=cname-error
    Run Keyword If    len('${email msg}') > 0    s2l.Element Text Should Be    id=cemail-error    ${email msg}
    ...    ELSE    s2l.Element Should Not Be Visible    id=cemail-error
    Run Keyword If    len('${comment msg}') > 0    s2l.Element Text Should Be    id=ccomment-error    ${comment msg}
    ...    ELSE    s2l.Element Should Not Be Visible    id=ccomment-error

Test Form Validation
    [Arguments]    ${name}    ${name msg}    ${email}    ${email msg}    ${comment}    ${comment msg}
    s2l.Open Browser    http://127.0.0.1:8000/
    Fill Form    ${name}    ${email}    ${comment}
    Should Message Be    ${name msg}    ${email msg}    ${comment msg}
    [Teardown]    s2l.Close All Browsers
